#!/usr/bin/perl -w

use strict;

our $opt_dry= $ENV{DRY};

$0=~ /([^\/]+)$/s;
my $me= $1;
sub usage {
    die "$me  list of packages to upgrade
  Checks each package whether it is really installed, and if yes,
  apt-get install's it.
";
    exit 1;
}

usage unless @ARGV;

# get list of installed packages
our %installed= map { chomp; $_=>1 } `dpkglist -i`;

$|=1;

our @packages= grep {
    if (/^-/) {
        usage
    } else {
	my $installed= $installed{$_};
	if (!$installed) {
	    print "$me: package '$_' is not installed.\n";
	}	
	$installed
    }
} @ARGV;

use Chj::Debian::DpkgAptList 'aptlist';
use Chj::Chomp;

use Chj::xperlfunc;

sub prln {
    print "$me: @_\n";
}

if (@packages) {
    # before upgrading packages, save the auto-install flags
    my %autoinstalled;
    aptlist( +{Package=>1,
	       'Auto-Installed'=>1,
	      },
	     sub {
		 my ($d)=@_;
		 my $name= Chomp $$d{Package}[0][0];
		 my $autoinstalled= Chomp $$d{'Auto-Installed'}[0][0];
		 $autoinstalled{$name}= $autoinstalled;
	     });

    my @autopackages= grep {
	$autoinstalled{$_}
    } @packages;

    my $save_autoflags= sub {
	if (@autopackages) {
	    prln "These packages had their auto-installed flag set:";
	    prln "  $_" for @autopackages;
	    prln "set their flags again..";
	    xxsystem( ($opt_dry ? "echo" : ()), "aptitude", "install", "--",
		      map {
			  $_.'&M'
		      } @autopackages);
	    prln "done";
	}
    };

    eval {
	xxsystem( ($opt_dry ? "echo" : ()), "apt-get", "install", "--", @packages);
	1
    } || do {
	my $e= $@;
	eval {
	    prln "There was an error.";
	    &$save_autoflags;
	};
	prln "Here's the previous error:";
	die $e;
    };
    &$save_autoflags
	
}


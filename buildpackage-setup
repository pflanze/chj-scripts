#!/usr/bin/perl -w

# Fre Dez 13 14:57:56 CET 2002

use strict;

$0=~ /([^\/]+)$/s or die "?";
my $myname=$1;

use Chj::xIOUtil qw(xgetfile_utf8 xputfile_utf8);
use Chj::Mailfrom 'mailfromaddress';
use Chj::userfullname;
use Chj::username;

sub usage {
    print "$myname

  Adds an entry to debian/changelog with the current username, email, time,
  and the current release name from deb-src in the apt sources.list.
";
exit @_;
}

my @files;
for (my $i=0; $i<=$#ARGV; $i++) {
    local $_=$ARGV[$i];
    if (/^--?h(elp)?$/) {
        usage
    } elsif ($_ eq '--') {
        push @files, @ARGV[$i+1..$#ARGV];
        last;
    } elsif (/^-/) {
        warn "Unknown option '$_'\n";
        usage(1)
    } else {
        push @files, $_
    }
}
usage if @files;

our $userfullname= userfullname;
our $initials= do {
    if ($userfullname eq "Christian Jaeger") {
	"cj"
    } else {
	username
    }
};
our $email= mailfromaddress;


my $a= xgetfile_utf8 "debian/changelog";

my ($first) = $a=~ /^(.*)(?:\n\S|$)/s
    or die "Strange format in changelog";

my ($firstuser) = $first=~ /\n -- ([^\n]*)/
    or die "invalid format";

if ($firstuser=~ /$userfullname/) {
    warn "debian/changelog seems to already have been changed, cancelling.\n";
    exit 0;
}

my ($firstline) = $a=~ /^([^\n]+)/s
    or die "invalid format";

$firstline=~ s/^([^()]*\([^()]*)\)/$1.$initials\)/
    or die "invalid format: missing version in parens";

my $date= `date -R`;

my $release= $ENV{DEBIAN_RELEASE} || do {
    my @release = (
        map {
            my @f = split /\s+/;
            $f[2]
        }
        grep {
            /^deb-src\s/
        }
        split /\n/,
        xgetfile_utf8 "/etc/apt/sources.list");
    @release == 1 ?
        $release[0]
        : die "expecting exactly one deb-src line in /etc/apt/sources.list, but got ".@release."; please provide DEBIAN_RELEASE env var with the release name instead";
};

$a= "$firstline

  * recompiled from $release

 -- $userfullname <$email>  $date
$a";

xputfile_utf8 "debian/changelog", $a;


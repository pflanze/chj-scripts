#!/usr/bin/perl -w

# Tue Nov 11 11:18:22 CET 2008
(my $email='christian-novo%jaeger,mine,nu')=~ tr/%,/@./;

use strict;


$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);

use Chj::App::Recordip;

our $datadir= datadir (0);
our $default_interface="eth0";

sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    my $datadir_path= $datadir->path;
    print "$myname [ interface(s) ]

  Look at the interfaces' IP's, if different from the last run of
  $myname, look them up in reverse dns and add them to the database
  (with the given environment: '$datadir_path').

  If no interfaces are given, just look at all relevant interfaces
  (i.e. excluding 'lo' and interface aliases) which are up.

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   ) or exit 1;

sub debug {
    print STDERR "$myname: ",join(" ", @_),"\n"
      if $verbose;
}

$datadir->make(0700);

use Chj::FileStore::MIndex;
our $ip_store= Chj::FileStore::MIndex->new($datadir->subdir_make("ip_store")->path);
use Chj::FileStore::PIndex;
our $attr_store= Chj::FileStore::PIndex->new ($datadir->subdir_make("attr_store")->path);

use Chj::DNS 'maybe_ip_reverse_lookup';

# ip -> domainname.

sub _resolveadd ($ ) {
    my ($ip)=@_;
    $ip_store->add ($ip,
		    maybe_ip_reverse_lookup($ip));
}

{
    package CHJ::recordip::ifaces;
    use Chj::Net::Ifaces -extend=>();
    sub is_relevant_interface {
	my $proto=shift;
	my ($if)=@_;
	not ($if eq "lo"
	     or
	     $if=~ /:/)
    }
}


sub recordips {
    my (@interfaces)=@_;
    my $ifaces= CHJ::recordip::ifaces->new_now (@interfaces);
    my $ips_string= $ifaces->ips_string;

    my $resolveadd= sub {
	for my $if_and_ip ($ifaces->if_and_ip_s) {
	    my ($interface,$ip)= @$if_and_ip;
	    debug "interface '$interface' is at ip '$ip'";
	    _resolveadd ($ip);
	} # "else" the missing interfaces. also an old  pattern.
	  # debug "interface '$interface' is not up"
          # [todo]
	$attr_store->set ("lastips",$ips_string);
    };

    if (my $lastips_string= $attr_store->maybe_get("lastips")) {
	if ($lastips_string eq $ips_string) {
	    debug "lastips is '$ips_string' already, so do not look-up&add-reverse-entry again";
	} else {
	    debug "ips '$ips_string' is different from lastips '$lastips_string'";
	    &$resolveadd
	}
    } else {
	debug "no lastip entry yet";
	&$resolveadd
    }
}

#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;
#__END__

if (@ARGV) {
    recordips @ARGV
} else {
    recordips ()
}


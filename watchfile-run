#!/usr/bin/perl -w

# Fre Sep  7 11:43:44 CEST 2007
(my $email='pflanze%gmx,ch')=~ tr/%,/@./;

use strict;

our $sleeptime= 0.3;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname cmd file(s)

  watch given file(s) for changes, if a change happens, run cmd (which is
  run using perl's system call thus can contain shell stuff).

  Options:
   -a  also run before waiting
   -t  show time stamp before running cmd

  Also see: 'C' and 'lambda' for building 'cmd'

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

my @args;
my $DEBUG=0;
our $verbose;
our $opt_a;
our $opt_t;
for (my $i=0; $i<=$#ARGV; $i++) {
    local $_=$ARGV[$i];
    if (/^--?h(elp)?$/) {
    	usage
    } elsif ($_ eq '--') {
    	push @args, @ARGV[$i+1..$#ARGV];
    	last;
    } elsif (/^--?d(ebug)?$/) {
	$DEBUG=1;
    } elsif (/^--?a$/) {
	$opt_a=1;
    } elsif (/^--?t$/) {
	$opt_t=1;
    } elsif (/^--?v(erbose)?$/) {
	$verbose=1;
    } elsif (/^-./) {
    	usage("Unknown option '$_'\n");
    } else {
    	push @args, $_
    }
}
usage unless @args>1;

use Time::HiRes 'sleep';
use Chj::xperlfunc;# 'xstat';
use Chj::Unix::Exitcode 'exitcode';

our $cmd=shift @args;
our @files= @args;

our @stats= map { scalar xstat $_ } @files;

$|++;

sub runit {
    if ($opt_t) {
        print "--".localtime()."-------------------------------------\n";
    }
    xsystem $cmd;
    if ($opt_t) {
        print(("-"x63)."\r");
    }
}

runit
    if $opt_a;

while (1) {
    sleep $sleeptime;
  CHECK: {
	for (my $i=0; $i<@files; $i++) {
	    local our $s= xstat $files[$i];
	    if ($s->equal ($stats[$i])) {
		# no change
	    } else {
		print "--- ".localtime().": '$files[$i]' has changed -------------\n" if $verbose;
		$stats[$i]=$s;
                runit;
                last CHECK;
	    }
	}
    }
}

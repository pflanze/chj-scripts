#!/usr/bin/perl -w

# Fri Apr 24 10:04:43 EDT 2009
(my $email='ch%christianjaeger,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname errno(s)

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   ) or exit 1;
usage unless @ARGV;

require Errno;

my %errno2str;
for my $str (@Errno::EXPORT_OK) {
    next unless $str=~ /^\w/;
    no strict 'refs';
    my $errno= &{"Errno::$str"} // die "exported sub '$str' from Errno is returning undef";
    $errno2str{$errno}= $str;
}

sub printerrno {
    my ($no)=@_;
    $!=$no; my $err= "$!";
    my $errsym= ($errno2str{$no} // "(no symbol defining $no)");
    print "$no == $errsym: $err\n"
      or die $!;
}

printerrno $_ for @ARGV;

#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;

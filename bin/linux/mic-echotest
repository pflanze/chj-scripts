#!/usr/bin/perl -w

# Tue May 26 18:11:00 EDT 2009
(my $email='christian-novo%jaeger,mine,nu')=~ tr/%,/@./;

use strict;

our $default_samplerate= 22050;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname [samplerate]

  Do a local echo test with the given sample rate (default: $default_samplerate)

  Options:
  --resample    do resampling in userspace
  -d|--device d    device for both recording and playback, e.g. hw:1,0

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
our $opt_resample;
our $opt_device;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "resample"=> \$opt_resample,
	   "device=s"=> \$opt_device,
	   ) or exit 1;
usage unless @ARGV<=1;

our $rate= @ARGV ? $ARGV[0] : $default_samplerate;

$rate=~ /^\d+\z/ or die "not a number '$rate'";

#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;

use Chj::xperlfunc;

our $len= int($rate*1.5);

sub _xxsystem ($ ) {
    print "@_\n";
    xxsystem @_
}

our $datrate= 48000;

our $use_oss4= -d "/proc/opensound";
our $pipesoundtype= ($use_oss4 ? "wav" : "raw");

our $recordcmd=
  ($use_oss4 ?
   sub {
       my ($maybe_rate)=@_;
       die "--device not implemented for OSS" if defined $opt_device;
       # $pipesoundtype ? no just hard code sgh
       "ossrecord -fS16_LE -s ".($maybe_rate||$datrate)." -c 1 - | "
   }
   :
   # alsa
   sub {
       my ($maybe_rate)=@_;
       ("arecord -t raw -f dat ".
	(defined ($opt_device) ? "-D $opt_device " : "").
	($maybe_rate ? "-r $maybe_rate" : "").
	" -c 1 | ")
   });

our $playcmd=
  ($use_oss4 ?
   sub {
       my ($maybe_rate)=@_;
       die "--device not implemented for OSS" if defined $opt_device;
       # use $pipesoundtype ? well non-raw, so let it autodetect it
       "ossplay -fS16_LE -s ".($maybe_rate||$datrate)." -c 1 - "
   }
   :
   # alsa
   sub {
       my ($maybe_rate)=@_;
       ("aplay -t raw -f dat ".
	(defined ($opt_device) ? "-D $opt_device " : "").
	($maybe_rate ? "-r $maybe_rate" : "").
	" -c 1 -")
   });


our $bfr = -e "/usr/bin/bfr" ?
  sub { "bfr @_" } :
  sub { warn "bfr not found, using cat instead"; "cat" };

if ($opt_resample) {
    our $semilen= int($len/2);
    our $prelen= int ($datrate*1.5/2);
    _xxsystem
      (&$recordcmd (undef).
       &$bfr("-m $prelen")." | ".
       "sox -t $pipesoundtype -r $datrate -s -2 -c 1 -  -t $pipesoundtype -r $rate -".
       #recoding:
       "   resample -ql |".
       &$bfr("-m $semilen")." | ".
       &$playcmd($rate));
} else {
    _xxsystem
      (&$recordcmd ($rate).
       &$bfr("-m $len")." | ".
       &$playcmd($rate));
}

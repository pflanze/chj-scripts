#!/usr/bin/perl -w

# Tue 28 Jul 18:40:13 BST 2020
(my $email='ch%christianjaeger,ch')=~ tr/%,/@./;

use strict; use warnings FATAL => 'uninitialized';

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname [opts] [patterns]

  Run locate with the given arguments except -F is dropped and when -F
  isn't given --regex is added. Also, if multiple patterns are given,
  paths that match all of them are shown instead of either of them
  (intersection instead of union) unless --union is given (note that
  you can also provide union semantivs via '|' regex patterns).

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

#use FP::Repl;


my %opt_has_val= (
    all=> 0,
    basename=> 0,
    count=> 0,
    database=> 1,
    existing=> 0,
    follow=> 0,
    #help
    'ignore-case'=> 0,
    limit=> 1,
    #mmap=>
    nofollow=> 0,
    null=> 0,
    statistics=> 0,
    quiet=> 0,
    regexp=> 1,
    regex=> 0,
    #stdio=>
    version=> 0,
    wholename=> 0,
    );

my %opt; # name => [ $argnum, $maybe_val ]

my $argnum= 0; # hacky

sub opt($) {
    my ($k)=@_;
    my $has_val= $opt_has_val{$k} //
        die "bug: missing opt_has_val for '$k'";
    sub {
        my ($whatsthishash, $arg)=@_;
        #repl;
        push @{$opt{$k}}, [ $argnum++, $arg];
    }
}

use Getopt::Long;
my $verbose=0;
my $opt_F;
my $opt_union;
my $opt_intersection;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
           "F"=> \$opt_F,
           "union"=> \$opt_union,
           "intersection"=> \$opt_intersection,
           "A|all"=> opt "all",
           "b|basename"=> opt "basename",
           "c|count"=> opt "count",
           "d|database=s"=> opt "database",##?
           "e|existing"=> opt "existing",
           "L|follow"=> opt "follow",
           "i|ignore-case"=> opt "ignore-case",
           "limit|n=n"=> opt "limit",
           "m|mmap"=> sub{ },
           "P|nofollow|H"=> opt "nofollow", #XX oppos of follow
           "0|null"=> opt "null",
           "S|statistics"=> opt "statistics",
           "q|quiet"=> opt"quiet",
           "r|regexp=s"=> opt "regexp", ##
           "regex"=> opt "regex",
           "stdio"=> sub { },
           "V|version"=> opt "version",
           "w|wholename"=> opt "wholename",
    ) or exit 1;

my @patterns= @ARGV;

my $regex_given= exists $opt{regex};

usage "both -F and --regex given"
    if ($opt_F and $regex_given);

usage "both --intersection and --union given"
    if ($opt_intersection and $opt_union);

my $is_union= $opt_union;


my @reconstructed_opt;
for my $k (keys %opt) {
    my $has_val= $opt_has_val{$k} // die "bug";
    for my $tupl (@{$opt{$k}}) {
        my ($argnum, $arg) = @$tupl;
        $reconstructed_opt[$argnum] and die "bug";
        $reconstructed_opt[$argnum] = [ "--$k", $has_val ? $arg : () ];
    }
}

my @opts= (
    ($opt_F or $regex_given) ? () : "--regex",
    (map {
        if (defined $_) {
            @$_
        } else {
            ()
        }
     } @reconstructed_opt),
    );

sub cmd {
    my (@patterns)= @_;
    (
     "locate", @opts, "--", @patterns
    )
}

# XX sigh, should also check for --regexp option(s) !

if ($is_union or @patterns <= 1) {
    exec cmd(@patterns)
        or exit 127; # which exit code was the right one?
} else {
    # same approach as in "multiggc", XX abstract....

    use lib "/opt/functional-perl/lib"; # for fixed version of Chj::IO::Command
    require Chj::IO::Command;

    my $maxfd= 99999; # ?

    my @qs= do {
        local $^F= $maxfd;
        map {
            scalar Chj::IO::Command->new_sender(cmd($_))
        } @patterns;
    };

    my @fds= map { $_->fileno } @qs;

    for (@fds) {
        die "too many file handles used" if $_ > $maxfd
    }

    exec "intersection", map { "/dev/fd/$_" } @fds
        or exit 127; # which exit code was the right one?

    #/ abstract
}

#repl;

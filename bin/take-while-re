#!/usr/bin/perl -w

(my $email='ch%christianjaeger,ch')=~ tr/%,/@./;

use strict; use warnings FATAL => 'uninitialized';

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname regex [ files.. ]

  Read lines from files or stdin and print to stdout as long as regex
  on them is true, then stop.

  Options:

    --invert  invert the boolean result of the regex match

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
our $opt_invert;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "invert"=> \$opt_invert,
	   ) or exit 1;

usage unless @ARGV;

my $restr= shift @ARGV;
my $re= qr/$restr/s;

while (<>) {
    last if (/$re/ ? $opt_invert : !$opt_invert);
    print or die "stdout: $!"
}

close STDOUT
  or die "stdout: $!";


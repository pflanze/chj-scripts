#!/usr/bin/perl -w

# Mon Apr 12 19:32:03 EDT 2010
(my $email='XXX%YYY,ch')=~ tr/%,/@./;

use strict;

$0=~ /(.*?)([^\/]+)\z/s or die "?";
my ($mydir, $myname)=($1,$2);
sub usage {
    print STDERR map{"$_\n"} @_ if @_;
    print "$myname pid

  Options:
   -s|--shell  shell-escape values and prepend lines with 'export '

  (Christian Jaeger <$email>)
";
exit (@_ ? 1 : 0);
}

use Getopt::Long;
our $verbose=0;
#our $opt_dry;
our $opt_shell;
GetOptions("verbose"=> \$verbose,
	   "help"=> sub{usage},
	   "shell"=> \$opt_shell,
	   #"dry-run"=> \$opt_dry,
	   ) or exit 1;
usage unless @ARGV==1;

use Chj::xopen 'xopen_read';
use Chj::singlequote 'singlequote_sh';

sub printenv_of {
    my ($pidstr)=@_;
    my ($pid)= $pidstr=~ /^(\d+)\z/
      or die "not a pid: $pidstr";
    my $in= xopen_read "/proc/$pid/environ";
    local $/="\0";
    while (<$in>) {
	chomp;
	if ($opt_shell) {
	    my ($k,$v)= split "=", $_, 2;
	    print "export $k=".singlequote_sh($v)."\n"
	      or die "stdout: $!";
	} else {
	    print "$_\n"
	      or die "stdout: $!";
	}
    }
    $in->xclose;
}

printenv_of $_
  for @ARGV;


#use Chj::ruse;
#use Chj::Backtrace; use Chj::repl; repl;

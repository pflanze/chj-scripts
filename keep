#!/usr/bin/perl -w

# Die Dez 31 02:43:40 MET 2002

use strict;

use Chj::Cwd::realpath qw(xrealpath);
use Chj::xperlfunc qw(xlinkunlink xxsystem dirname basename xprintln);

$0=~ /([^\/]+)$/s or die "?";
my $myname=$1;

sub usage {
    print "$myname file(s)
  Keeps a version of the mentioned file(s).
  file.1 file.2 file.3 etc., by default it increments the number
  for each new file and leaves the filenames of existing kept
  versions unchanged.
  Options:
  -r --rotate  Rotate file numbers instead of sequential, so that
               the oldest version has the highest number.
  -a appendix  Append this to each filename (after the number)
  -t           turn files around: move file to kept name, then copy it
               back to normal name. (Default is, to copy it from normal
               name to kept name.)
  -v           output the filename choosen for the newly kept version(s)

  Notes:
  With -r, if there are holes in the numbering chain, it doesn't matter,
  it will move the numbers all the same.
";
    exit @_;
}

my @files;
my $DEBUG=0;
my $opt_r;
my $opt_append="";
my $opt_t;
our $verbose;
for (my $i=0; $i <= $#ARGV; $i++) {
    local $_=$ARGV[$i];
    if (/^--?h(elp)?$/) {
    	usage
    } elsif ($_ eq '--') {
    	push @files, @ARGV[$i+1..$#ARGV];
    	last;
    } elsif (/^--?d(ebug)?$/) {
	$DEBUG=1;
    } elsif (/^--?r(otate)?$/) {
	$opt_r=1;
    } elsif (/^--?a(ppend)?$/) {
	$opt_append= $ARGV[++$i] or usage("--append option requires argument");
    } elsif (/^--?t$/) {
	$opt_t=1;
    } elsif (/^--?v(erbose)?$/) {
	$verbose=1;
    } elsif (/^-/) {
    	warn "Unknown option '$_'\n";
    	usage(1)
    } else {
    	push @files, $_
    }
}
usage unless @files;

my $inbetween= ".";

sub xrename($$) {
    my ($a, $b)= @_;
    if (-e $b) {
        die "target '$b' exists, will not overwrite!\n";
    }
    rename $a, $b
        or die "could not rename '$a' to '$b': $!\n";
}

sub xcopy($$) {
    my ($a, $b)= @_;
    if (-e $b) {
        die "target '$b' exists, will not overwrite!\n";
    }
    if ($opt_t) {
	# xlinkunlink $a, $b; doesn't work with grsecurity.
        # We already checked target path, so can ~safely do:
	xrename $a, $b;
	xxsystem "/bin/cp", "-a", "--", $b, $a;
    } else {
	xxsystem "/bin/cp", "-a", "--", $a, $b;
    }
    xprintln $b if $verbose;
}

sub _cleaneditem {
    my ($item)= @_; # might also be a dir
    xrealpath $item;
}

sub rotate($) {
    my $item= _cleaneditem(@_);
    my $dir= dirname($item);
    my $itemname= basename($item);
    opendir DIR, $dir
        or die "could not open dir '$dir' for reading: $!";
    my %takennumbers;
    while (defined (local $_= readdir DIR)) {
	if (/^\Q$itemname\E\.(\d{1,4})(?:_|\b)/s) {
	    # found a (probable) backup item.
	    if (exists $takennumbers{$1}) {
		die "'$_' uses same backup number as '$takennumbers{$1}'";
	    }
	    $takennumbers{$1}= $_;
	}
    }
    closedir DIR or die "error closing dir: $!";
    for (sort { $b <=> $a } keys %takennumbers) {
	my ($newname, $oldname);
	$oldname= $newname= $takennumbers{$_};
	my $newnumber= $_+1;
	$newname=~ s/(\Q$itemname\E\.)\d+(.*)/$1$newnumber$2/s
            or die "BUG";
	xrename $oldname, $newname;
    }
    xcopy $item, "$item.1$opt_append";
}

sub keep($) {
    my $item= _cleaneditem(@_);
    my $dir= dirname($item);
    my $itemname= basename($item);
    opendir DIR, $dir
        or die "could not open dir '$dir' for reading: $!";
    my $usenumber=1;
    while (defined (local $_= readdir DIR)) {
	if (/^\Q$itemname\E\.(\d{1,4})(?:_|\b)/s) {
	    # found a backup item.
	    if ($1 >= $usenumber){
		$usenumber= $1+1;
	    }
	}
    }
    xcopy $item, "$item.$usenumber$opt_append";
}

for (@files) {
    $opt_r ? rotate($_) : keep($_);
}

